---
:parameters:
  - 
    :name: "Terminology"
    :description: "The terminology for which the export is required."
    :type: :thesauri
:columns:
  :clln:
    :label: CODELIST_LONG_NAME
    :type: literal
  :clc:
    :label: CODELIST_CODE
    :type: literal
  :clsn:
    :label: CODELIST_SHORT_NAME
    :type: literal
  :cle:
    :label: CODELIST_EXTENSIBLE
    :type: literal
  :cld:
    :label: CODELIST_DEFINITION
    :type: literal
  :code:
    :label: CODE
    :type: literal
  :decode:
    :label: DECODE
    :type: literal
  :svalue:
    :label: SUBMISSION_VALUE
    :type: literal
  :synonym:
    :label: SYNONYM
    :type: literal
  :crfdv:
    :label: CRF_DISPLAY_VALUE
    :type: literal
  :displayorder:
    :label: DISPLAY_ORDER
    :type: literal
  :rank:
    :label: RANK
    :type: literal
  :dc_stage:
    :label: DC_STAGE
    :type: literal
  :sdtm_stage:
    :label: SDTM_STAGE
    :type: literal
  :adam_stage:
    :label: ADAM_STAGE
    :type: literal
:label: Sponsor CT Export
:query: 'PREFIX th: <http://www.assero.co.uk/Thesaurus#>
PREFIX isoI: <http://www.assero.co.uk/ISO11179Identification#> 
PREFIX isoR: <http://www.assero.co.uk/ISO11179Registration#> 
PREFIX isoC: <http://www.assero.co.uk/ISO11179Concepts#> 
PREFIX isoT: <http://www.assero.co.uk/ISO11179Types#> 
PREFIX bo: <http://www.assero.co.uk/BusinessOperational#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> 
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> 
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#> 
PREFIX skos: <http://www.w3.org/2004/02/skos/core#> 
  
SELECT ?clln ?clc ?clsn ?cle ?cld ?code ?decode ?svalue (GROUP_CONCAT(DISTINCT ?clisyn;separator=";") as ?synonym) ?crfdv ?displayorder ?rank ?dc_stage ?sdtm_stage ?adam_stage WHERE  
{
  { 
    VALUES ?s {[[[parameter_1]]]}
    ?s th:isTopConceptReference/bo:reference ?cl .
    FILTER (NOT EXISTS {?cl th:isOrdered ?x})
    ?cl th:identifier ?clc .  
    ?cl th:notation ?clsn .
    ?cl th:extensible ?cle .
    ?cl th:definition ?cld .
    ?cl th:preferredTerm/isoC:label ?clln .
    
    {
      FILTER (EXISTS {?cl ^th:pairedWith ?y})
      ?cl th:narrower ?cli .
      ?cli th:definition ?clid .
      ?cl ^th:pairedWith ?clp .
      ?clp th:narrower ?clpi .
      ?clpi th:definition ?clid .
      ?clpi th:identifier ?code .  
    } 
    UNION 
    {
      FILTER (EXISTS {?cl th:pairedWith ?y})
      ?cl th:narrower ?cli .
      ?cli th:definition ?clid .
      ?cli th:identifier ?code .  
    }
    UNION
    {
      FILTER (NOT EXISTS {?cl th:pairedWith ?y} && NOT EXISTS {?cl ^th:pairedWith ?z})
      ?cl th:narrower ?cli .
      ?cli th:identifier ?code .  
    }

    ?cli th:preferredTerm/isoC:label ?decode .
    ?cli th:notation ?svalue .

    ?cli ^isoC:appliesTo ?ext1 .
    ?ext1 isoC:context ?cl . 
    ?ext1 isoC:customPropertyDefinedBy/isoC:label "SDTM Stage" .
    ?ext1 isoC:value ?sdtm_stage .
    ?cli ^isoC:appliesTo ?ext2 .
    ?ext2 isoC:context ?cl . 
    ?ext2 isoC:customPropertyDefinedBy/isoC:label "DC Stage" .
    ?ext2 isoC:value ?dc_stage .
    ?cli ^isoC:appliesTo ?ext3 .
    ?ext3 isoC:context ?cl . 
    ?ext3 isoC:customPropertyDefinedBy/isoC:label "ADaM Stage" .
    ?ext3 isoC:value ?adam_stage .
    ?cli ^isoC:appliesTo ?ext4 .
    ?ext4 isoC:context ?cl . 
    ?ext4 isoC:customPropertyDefinedBy/isoC:label "CRF Display Value" .
    ?ext4 isoC:value ?crfdv .

    BIND ("" AS ?displayorder)
    OPTIONAL {?cli th:synonym/isoC:label ?clisyn .}
    OPTIONAL 
    {
      ?cli ^(th:item) ?rank_member . 
      FILTER (EXISTS {?cl th:isRanked/th:members/th:memberNext* ?rank_member })  
      ?rank_member th:rank ?rank 
    }
  }
  UNION
  {
    {
      SELECT ?cl ?cli (COUNT(?mid) as ?displayorder) WHERE 
      {
        VALUES ?s {[[[parameter_1]]]}
        ?s th:isTopConceptReference/bo:reference ?cl .
        ?cl th:isOrdered/th:members/th:memberNext* ?mid . 
        ?mid th:memberNext* ?m .
        ?m th:item ?cli .
      } GROUP BY ?cl ?cli ORDER BY ?cl
    }
    ?cl th:subsets ?clp .
    ?clp th:identifier ?clc .  
    ?cl th:notation ?clsn .
    ?cl th:extensible ?cle .
    ?cl th:definition ?cld .
    ?cl th:preferredTerm/isoC:label ?clln .
    ?cli th:identifier ?code .  
    ?cli th:preferredTerm/isoC:label ?decode .
    ?cli th:notation ?svalue .

    ?cli ^isoC:appliesTo ?ext1 .
    ?ext1 isoC:context ?cl . 
    ?ext1 isoC:customPropertyDefinedBy/isoC:label "SDTM Stage" .
    ?ext1 isoC:value ?sdtm_stage .
    ?cli ^isoC:appliesTo ?ext2 .
    ?ext2 isoC:context ?cl . 
    ?ext2 isoC:customPropertyDefinedBy/isoC:label "DC Stage" .
    ?ext2 isoC:value ?dc_stage .
    ?cli ^isoC:appliesTo ?ext3 .
    ?ext3 isoC:context ?cl . 
    ?ext3 isoC:customPropertyDefinedBy/isoC:label "ADaM Stage" .
    ?ext3 isoC:value ?adam_stage .
    ?cli ^isoC:appliesTo ?ext4 .
    ?ext4 isoC:context ?cl . 
    ?ext4 isoC:customPropertyDefinedBy/isoC:label "CRF Display Value" .
    ?ext4 isoC:value ?crfdv .

    OPTIONAL {?cli th:synonym/isoC:label ?clisyn .}
    OPTIONAL 
    {
      ?cli ^(th:item) ?rank_member . 
      FILTER (EXISTS {?cl th:isRanked/th:members/th:memberNext* ?rank_member })  
      ?rank_member th:rank ?rank 
    }
  }
} GROUP BY ?clln ?clc ?clsn ?cle ?cld ?code ?decode ?svalue ?crfdv ?displayorder ?rank ?dc_stage ?sdtm_stage ?adam_stage ORDER BY ?clisyn ?clc ?code ?displayorder ?rank'
:type: Ad Hoc Report Definition