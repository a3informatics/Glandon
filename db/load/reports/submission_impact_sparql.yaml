---
:parameters:
  - 
    :name: "Terminology"
    :description: "The terminology for which the submission impact is required."
    :type: :thesauri
:columns:
  :sl:
    :label: Sponsor Code List Label
    :type: literal
  :si:
    :label: Sponsor Code List Identifier
    :type: literal
  :sclil:
    :label: Sponsor Code List Item Label
    :type: literal
  :sclii:
    :label: Sponsor Code List Item Identifier
    :type: literal
  :sn:
    :label: The Notation
    :type: literal
  :cll:
    :label: CDISC Code List Label
    :type: literal
  :cli:
    :label: CDISC Code List Identifier
    :type: literal
  :clil:
    :label: CDISC Code List Item Label
    :type: literal
  :clii:
    :label: CDISC Code List Item Identifier
    :type: literal
  :ctv:
    :label: CDISC Terminology Version
    :type: literal
  :t:
    :label: Type
    :type: literal
:label: Submission Impact Export
:query: 'PREFIX th: <http://www.assero.co.uk/Thesaurus#> 
PREFIX bo: <http://www.assero.co.uk/BusinessOperational#> 
PREFIX isoI: <http://www.assero.co.uk/ISO11179Identification#> 
PREFIX isoR: <http://www.assero.co.uk/ISO11179Registration#> 
PREFIX isoC: <http://www.assero.co.uk/ISO11179Concepts#> 
PREFIX isoT: <http://www.assero.co.uk/ISO11179Types#> 
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> 
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> 
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#> 
PREFIX skos: <http://www.w3.org/2004/02/skos/core#> 
SELECT DISTINCT ?sl ?si ?sclil ?sclii ?sn ?cll ?cli ?clil ?clii ?ctv ?t WHERE 
{ 
  VALUES ?th {[[[parameter_1]]]} 
  { 
    { 
      SELECT DISTINCT ?ct ?ctv WHERE 
      { 
        ?th th:reference/bo:reference ?refct . 
        ?refct isoT:hasIdentifier/isoI:version ?v. 
        ?refct isoT:hasIdentifier/isoI:identifier ?ident. 
        ?ct isoT:hasIdentifier/isoI:identifier ?ident . 
        ?ct isoT:hasIdentifier/isoI:version ?ctv . 
        FILTER (?ctv > ?v ) 
      } 
    }
    ?th (th:isTopConceptReference/bo:reference) ?scl .
    {    
      FILTER (
        NOT EXISTS { ?scl isoT:hasState/isoR:byAuthority/isoR:owner false } &&         
        EXISTS { ?scl isoT:hasState/isoR:byAuthority/isoR:owner true} 
      ) 
      ?scl th:notation ?sn . 
      ?cl th:notation ?sn .
      FILTER (?scl != ?cl)
      ?cl rdf:type th:ManagedConcept .
      ?cl isoC:label ?cll . 
      ?cl th:identifier ?cli . 
      BIND ("Code List" as ?t) 
      BIND ("" as ?sclil)
      BIND ("" as ?sclii)
      BIND ("" as ?clii)
      BIND ("" as ?clil)
    } 
    UNION 
    { 
      ?scl th:narrower ?scli .
      FILTER
      (
        NOT EXISTS { ?scli ^th:narrower/isoT:hasState/isoR:byAuthority/isoR:owner false } && 
        EXISTS { ?scli ^th:narrower/isoT:hasState/isoR:byAuthority/isoR:owner true } 
      ) 
      ?scli th:notation ?sn . 
      ?clis th:notation ?sn . 
      FILTER (?scli != ?clis)
      ?clis rdf:type th:UnmanagedConcept .
      ?clis isoC:label ?clil . 
      ?clis th:identifier ?clii . 
      ?scli isoC:label ?sclil . 
      ?scli th:identifier ?sclii . 
      ?clis ^th:narrower ?x .
      ?x isoC:label ?cll . 
      ?x th:identifier ?cli . 
      BIND ("Code List Item" as ?t) 
    } 
    ?scl isoC:label ?sl . 
    ?scl th:identifier ?si . 
  } 
}'
:type: Ad Hoc Report Definition