---
:parameters:
  - 
    :name: "Terminology"
    :description: "The terminology for which the export is required."
    :type: :thesauri
:columns:
  :clln:
    :label: CODELIST_LONG_NAME
    :type: literal
  :clc:
    :label: CODELIST_CODE
    :type: literal
  :clsn:
    :label: CODELIST_SHORT_NAME
    :type: literal
  :cle:
    :label: CODELIST_EXTENSIBLE
    :type: literal
  :cld:
    :label: CODELIST_DEFINITION
    :type: literal
  :code:
    :label: CODE
    :type: literal
  :dcode:
    :label: DECODE
    :type: literal
  :svalue:
    :label: SUBMISSION_VALUE
    :type: literal
  :synonym:
    :label: SYNONYM
    :type: literal
  :crfdv:
    :label: CRF_DISPLAY_VALUE
    :type: literal
  :displayorder:
    :label: DISPLAY_ORDER
    :type: literal
  :rank:
    :label: RANK
    :type: literal
  :dc_stage:
    :label: DC_STAGE
    :type: literal
  :sdtm_stage:
    :label: SDTM_STAGE
    :type: literal
  :adam_stage:
    :label: ADAM_STAGE
    :type: literal
:label: Sponsor CT Subsets Export
:query: 'PREFIX th: <http://www.assero.co.uk/Thesaurus#>
PREFIX isoI: <http://www.assero.co.uk/ISO11179Identification#> 
PREFIX isoR: <http://www.assero.co.uk/ISO11179Registration#> 
PREFIX isoC: <http://www.assero.co.uk/ISO11179Concepts#> 
PREFIX isoT: <http://www.assero.co.uk/ISO11179Types#> 
PREFIX bo: <http://www.assero.co.uk/BusinessOperational#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> 
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> 
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#> 
PREFIX skos: <http://www.w3.org/2004/02/skos/core#> 
  
SELECT ?clln ?clc ?clsn ?cle ?cld ?code ?decode ?svalue (GROUP_CONCAT(DISTINCT ?clisyn;separator=";") as ?synonym) ?crfdv ?displayorder ?rank ?dc_stage ?sdtm_stage ?adam_stage WHERE  
{
  {
    SELECT ?cl ?cli (COUNT(?mid) as ?displayorder) WHERE 
    {
      VALUES ?s {[[[parameter_1]]]}
      ?s th:isTopConceptReference/bo:reference ?cl .
      ?cl th:isOrdered/th:members/th:memberNext* ?mid . 
      ?mid th:memberNext* ?m .
      ?m th:item ?cli .
    } GROUP BY ?cl ?cli ORDER BY ?cl
  }
  ?cl th:subsets ?clp .
  ?clp th:identifier ?clc .  
  ?cl th:notation ?clsn .
  ?cl th:extensible ?cle .
  ?cl th:definition ?cld .
  ?cl th:preferredTerm/isoC:label ?clln .
  ?cli th:identifier ?code .  
  ?cli th:preferredTerm/isoC:label ?decode .
  ?cli th:notation ?svalue .
  OPTIONAL {?cli th:synonym/isoC:label ?clisyn .}
  ?cli isoC:label ?crfdv
  OPTIONAL 
  {
    ?cli ^(th:item) ?rank_member . 
    FILTER (EXISTS {?cl th:isRanked/th:members/th:memberNext* ?rank_member })  
    ?rank_member th:rank ?rank 
  }
  BIND (EXISTS {?cli isoC:tagged/isoC:prefLabel "DC Stage"} AS ?dc_stage)
  BIND (EXISTS {?cli isoC:tagged/isoC:prefLabel "SDTM Stage"} AS ?sdtm_stage)
  BIND (EXISTS {?cli isoC:tagged/isoC:prefLabel "ADaM Stage"} AS ?adam_stage)
} GROUP BY ?clln ?clc ?clsn ?cle ?cld ?code ?decode ?svalue ?crfdv ?displayorder ?rank ?dc_stage ?sdtm_stage ?adam_stage ORDER BY ?clc ?code ?displayorder ?rank'
:type: Ad Hoc Report Definition